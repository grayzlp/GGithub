apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())


android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.grayzlp.ggithub"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode gitCommitCount
        versionName gitTag
        archivesBaseName = 'ggithub'
        resConfig "en"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "GITHUB_CLIENT_ID", "\"${github_client_id}\""
        buildConfigField "String", "GITHUB_CLIENT_SECRET", "\"${github_client_secret}\""
        buildConfigField "String", "GITHUB_CLIENT_CALLBACK", "\"${github_client_callback}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.legacy:legacy-support-v13:${versions.androidx}"
    implementation "androidx.palette:palette-ktx:${versions.androidx}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.cardview:cardview:${versions.androidx}"
    implementation "androidx.dynamicanimation:dynamicanimation:${versions.androidx}"

    // ButterKnife
    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"

    // Api
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.google.code.gson:gson:${versions.gson}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.google.guava:guava:${versions.guava}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.loggingInterceptor}"

    // Glide
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${versions.glide}"
    implementation "jp.wasabeef:glide-transformations:${versions.glideTransformations}"

    implementation("com.github.bumptech.glide:recyclerview-integration:${versions.glideRecyclerView}") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    // Dagger
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // UI
    implementation "com.afollestad.material-dialogs:core:${versions.materialDialog}"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxandroid}"

    // Other
    implementation "de.hdodenhof:circleimageview:${versions.circleImageview}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"


    // javaX Annotation
    implementation "javax.annotation:javax.annotation-api:${versions.javax}"
    annotationProcessor "javax.annotation:javax.annotation-api:${versions.javax}"
}
